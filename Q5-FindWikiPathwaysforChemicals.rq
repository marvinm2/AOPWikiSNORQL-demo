#This query takes all Stressor chemicals in the AOP-Wiki and their mappings to ChEBI IDs 
#Then, these ChEBI IDs are used to look up human molecular pathways in WikiPathways
PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
SELECT DISTINCT ?ChemicalName ?LinkedAOPURI ?ChEBI (STR(?PathwayID) AS ?Pathway_ID) 
(STR(?PathwayTitle) AS ?PathwayName) ?PathwayURI 
WHERE{
   ?chemid a cheminf:000407 ; 
      cheminf:000407 ?ChEBI .
   ?cheLook a cheminf:000000 ; 
      dc:title ?ChemicalName ;
      dcterms:isPartOf ?LinkedStressor ;
      skos:exactMatch ?chemid .
   ?LinkedStressor dcterms:isPartOf ?LinkedAOPURI .
   ?LinkedAOPURI a aopo:AdverseOutcomePathway ; 
      rdfs:label ?LinkedAOP .
   SERVICE <http://sparql.wikipathways.org/sparql>{ 
     ?metabolite wp:bdbChEBI ?chemid ; 
        dcterms:isPartOf ?PathwayURI.
     ?PathwayURI a wp:Pathway ; 
        dcterms:identifier ?PathwayID ; 
        dc:title ?PathwayTitle ;
        wp:organismName "Homo sapiens"^^xsd:string .}}

